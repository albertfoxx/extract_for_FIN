@echo off
REM Указываем кодировку. Необходимо для компьютерах, где по-умолчанию используется другая кодировка
chcp 866
echo.
REM author : albafoxx (abzaev.albert@gmail.com)

REM Название программы. Версия и дата релиза
set "titleName=Extract v4.2.15 (2022.05.22) - автоматический сборщик стенда АЦК-Финансы"
title %titleName%

REM ОПРЕДЕЛЯЕМ СТАНДАРТНЫЕ ПЕРЕМЕННЫЕ/ПУТИ
Set "SYS=АЦК-Финансы"
Set "ini=_extract_fin.ini"
Set "logfile=%CD%\extract_build.log"

Set "infile_1=StartServer.bat"
Set "infile_2=xml.properties"
Set "infile_3=Azk2Clnt.ini"
Set "infile_4=Azk2Server.properties"
Set "infile_5=web.properties"
Set "infile_6=start.bat"
Set "infile_7=server.xml"

Set "SP_PORT=2001"
Set "RMI_PORT=2099"
Set "fb_old=3050:D:/_DBase/balagan/R_MK.FDB"
Set "ora_old=F_IRKOBL_190327"
Set "ora_url_old=172.26.24.9:1521:ORACL11B"
Set "Jram_old=1024m"
Set "Server_old=8005"
Set "Connector_old=8080"

REM Выносим в начало скрипта, так как в 4 пункте проверки вопрос будет меняться
set question="Продолжить выполнение? [y/n]: "
REM Задаем значение "по-умолчанию"
set answer2=0

REM Задаем значение "по-умолчанию" для проверки успешности завершения сборки и записи в лог
set success=0

REM ВЫТАСКИВАЕМ ПЕРЕМЕННЫЕ/ПУТИ ИЗ ФАЙЛА НАСТРОЕК
setlocal DisableDelayedExpansion
set /a c=0
for /f "UseBackQ Delims=" %%A IN (%ini%) do (
  set /a c+=1
  setlocal EnableDelayedExpansion
  If !c!==1 (setlocal DisableDelayedExpansion
			 REM 1. директория откуда берем файл
			 set "in=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==2 (setlocal DisableDelayedExpansion
			 REM 2. директория где собираем стенд
			 set "out=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==3 (setlocal DisableDelayedExpansion
			 REM 3. директория EIS
			 set "EIS=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==4 (setlocal DisableDelayedExpansion
			 REM 4. директория ОТЧЕТЫ
			 set "REPORT=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==5 (setlocal DisableDelayedExpansion
			 REM 5. директория СОФИТ
			 set "SOFIT=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==6 (setlocal DisableDelayedExpansion
			 REM 6. директория Delphi_XE4_Runtime_BPLs
			 set "DelphiXE=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==7 (setlocal DisableDelayedExpansion
			 REM 7. директория с дополнительными файлами для сборки
			 set "schplanstop=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==8 (setlocal DisableDelayedExpansion
			 REM REM 8. директория с дополнительными файлами для сборки
			 set "azka=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==9 (setlocal DisableDelayedExpansion
			 REM 9. директория ТОМКАТом
			 set "tomcat=%%A"
			 setlocal EnableDelayedExpansion
			 endlocal)
  If !c!==10 (REM 10. Порт СП
			  set "renSP=%%A")
  If !c!==11 (rem 11. Порт Томката
			  set "renRMI=%%A")
  If !c!==12 (setlocal DisableDelayedExpansion
			  rem 12. директория до архиватора
			  set "winRar=%%A"
			  setlocal EnableDelayedExpansion
			  endlocal)
  If !c!==13 (rem 13. Путь до БД Firebird
			  set "fbNew=%%A")
  If !c!==14 (rem 14. Путь до БД Oracle
			  set "oraNew=%%A")
  If !c!==15 (rem 14.1 Путь до БД Oracle
			  set "oraUrlNew=%%A")
  If !c!==16 (rem 15. Размер Java-памяти
			  set "javaNew=%%A")
  If !c!==17 (rem 16. Порт на котором запускается веб-сервер
			  set "ServerNew=%%A")
  If !c!==18 (rem 17. Порт по которому можно будет открыть веб-клиент
			  set "ConnectorNew=%%A")
  If !c!==19 (rem 18. Список XML-файлов, в которых нужно изменить
			  set "listXML=%%A")
)

REM Разбираем строку со списком XML и записываем в отдельные переменные varN
setlocal enabledelayedexpansion
set n=0
for %%i in (%listXML%) do (
	set /a n=!n! + 1
    set var!n!=%%i
)

REM ИНФОРМАТИВНЫЙ БЛОК ДЛЯ ВЫВОДА В КОНСОЛЬ
SetLocal DisableDelayedExpansion
echo Директории:
echo берем эталонку из %in%
echo собираем здесь    %out%
echo.
echo -----------------------------------------------------------------------
echo Компоненты для сборки:
echo ЕИС	  %EIS%
echo Отчеты	  %REPORT%
echo СОФИТ	  %SOFIT%
echo ТОМКАТ	  %tomcat%
echo DelphiXE  %DelphiXE%
echo -----------------------------------------------------------------------
echo.

@echo Время сборки:     %DATE% %time:~0,8%>%logfile%
@echo Собираем сборку в %out%>>%logfile%
@echo.>>%logfile%
@echo Берем эталонку из		%in%>>%logfile%
@echo Используется ЕИС		%EIS%>>%logfile%
@echo Используется Отчеты 	%REPORT%>>%logfile%
@echo Используется СОФИТ		%SOFIT%>>%logfile%
@echo Используется ТОМКАТ		%tomcat%>>%logfile%
@echo Используется DelphiXE	%DelphiXE%>>%logfile%
@echo.>>%logfile%

REM ========== 1.БЛОК С ПРОВЕРКАМИ (начало) ==========
REM 1.1.ПРОВЕРКА ОСНОВНЫХ ДИРЕКТОРИЙ
:check_dir
set check_stop=0
echo Результат проверки указанных значений:
echo [Директория %in%]
If Exist "%in%" (call :writer 2 "+ Директория с эталонкой найдена"
) else (call :writer 4 "- Директория с эталонкой не найдена"
		set check_stop=1)
echo [Директория %EIS%]
If Exist "%EIS%" (call :writer 2 "+ Директория с ЕИС найдена"
) else (call :writer 4 "- Директория с ЕИС не найдена"
		set check_stop=1)
echo [Директория %REPORT%]
If Exist "%REPORT%" (call :writer 2 "+ Директория с отчетами найдена"
) else (call :writer 4 "- Директория с отчетами не найдена"
		set check_stop=1)
echo [Директория %SOFIT%]
If Exist "%SOFIT%" (call :writer 2 "+ Директория с софитом найдена"
) else (call :writer 4 "- Директория с софитом не найдена"
		set check_stop=1)
echo [Директория %tomcat%]
If Exist "%tomcat%" (call :writer 2 "+ Директория с томкатом найдена"
) else (call :writer 4 "- Директория с томкатом не найдена"
		set check_stop=1)
echo [Директория %DelphiXE%]
If Exist "%DelphiXE%" (call :writer 2 "+ Директория с DelphiXE найдена"
) else (call :writer 4 "- Директория с DelphiXE не найдена"
		set check_stop=1)

REM ЕСЛИ ХОТЬ ОДИН ПУТЬ УКАЗАН НЕКОРРЕКТНО - ОСТАНАВЛИВАЕМ СБОРКУ. ВЫДАЕМ СООБЩЕНИЕ.
echo.
If %check_stop%==0 (call :writer E "Все указанные директории найдены. Продолжаем сборку стенда %SYS%"
				   @echo.>>%logfile%
				   goto :check_out
	) else (call :writer 4 "Некорретно указаны директории, необходимо проверить пути в ini-файле [подробности выше]. Сборка стенда %SYS% остановлена."
			@echo.>>%logfile%
			set success=1
			goto :exit_bat)

REM 1.2.ПРОВЕРКА НАЛИЧИЯ ДИРЕКТОРИИ, В КОТОРОЙ БУДЕТ СОБИРАТЬСЯ СБОРКА
:check_out
If Not Exist "%out%" (
echo.
call :writer E "Директория, где собирается стенд не найдена. Она будет создана."
goto :extract
) else (
	echo.
	call :writer E "Директория, где собирается стенд уже создана. Подтвердите перезапись файлов..."
	goto :ask)

REM 1.3.ПРОВЕРКА НА ПЕРЕЗАПИСЬ ФАЙЛОВ В ПАПКЕ %OUT%, ЕСЛИ ОНА УЖЕ ЕСТЬ (СОЗДАНА)
:ask
set /p ask="Подтвердите перезапись файлов в директории "%out%"? [y/n]: "
@echo %DATE% %time:~0,8% : Подтвердите перезапись файлов в директории "%out%"? [y/n]: %ask%>>%logfile%
If %ask%==y (RD /S /Q %out%
			set success=0
			@echo %DATE% %time:~0,8% : Выполняется очистка директории "%out%" и повторная сборка АЦК в указанном каталоге...>>%logfile%
			goto :extract
) else (If %ask%==n (
		echo Сборка стенда %SYS% остановлена.
		@echo %DATE% %time:~0,8% : Сборка стенда остановлена. Директория "%out%" уже была создана ранее...>>%logfile%
		set success=1
		goto :exit_bat
		) else (echo Команда не распознана. Попробуйте ещё раз.
				@echo %DATE% %time:~0,8% : Команда не распознана. Попробуйте ещё раз...>>%logfile%
				set success=0
				echo.
				goto :ask))

REM 1.4.ВОПРОС НА ПРОДОЛЖЕНИЕ СБОРКИ СТЕНДА, ЕСЛИ ВОЗНИКНЕТ ОШИБКА НА ОПРЕДЕЛЕННОМ ЭТАПЕ
:askexit
@echo %DATE% %time:~0,8% : %question% - %ask% >>%logfile%
set /p ask=%question%
If %ask%==y (
	set success=1
	goto %answer%
	) else (
		If %ask%==n (
			If %answer2%==ask_start (
				set success=0
				goto :ask_start
				) else (
				set success=0
				goto :exit_bat)
		) else (
		call :writer E "Команда не распознана. Попробуйте ещё раз."
		goto :askexit))
REM ========== 1.БЛОК С ПРОВЕРКАМИ (конец) ==========

REM ========== 2.ОБЩИЕ КОМАНДЫ (начало) ==========
REM 2.1 ОБЩИЙ ПОРЯДОК КОМАНД ПРИ РАСПАКОВКЕ АРХИВОВ (начало)
:unzip_data
echo %textblock%
%winRar% x "%zipFile%" -o "%outputFile%" -r -y -ibck
call :check_error
echo -----------------------------------------------------------------------

REM 2.2.ОБЩИЙ ПОРЯДОК КОМАНД ПРИ КОПИРОВАНИИ ФАЙЛОВ (начало)
:copy_data
echo %textblock%
xcopy "%inputFolder%" "%outputFolder%" /i /e /h /y
call :check_error
echo -----------------------------------------------------------------------

REM 2.3 ОБЩИЙ ПОРЯДОК КОМАНД ПРИ ЗАМЕНЕ ДАННЫХ (начало)
:rename_data
SetLocal EnableExtensions EnableDelayedExpansion
REM Предварительно удаляем строки с пустыми значениями (фикс от записи текста "Режим вывода команд на экран (ECHO) отключен.")
echo %fileDirectory%
cd /d "%fileDirectory%"
findstr /vrc:"^$" /vrc:"^ $" /vrc:"^  $" %fileName%>tmp0.tmp
set "tempFile=tmp0.tmp"

echo %textblock% %fileName%
<"%tempFile%" >"tmp1.tmp" (
  for /f %%a in ('find /c /v "" ^< "%tempFile%"') do for /L %%C in (1,1,%%a) do (
    set LINE=& set /p LINE=
    if defined LINE (echo !LINE:%oldText%=%newText%!) else echo.
  )
)
del %tempFile%
move tmp1.tmp %fileName%
SetLocal DisableDelayedExpansion
call :check_error

REM 2.4 БЛОК ПОДПРОГРАММЫ ДЛЯ ПОДСВЕТКИ ВЫВОДА (начало)
:color
set c=%1& exit/b
 
:echo
for /f %%i in ('"prompt $h& for %%i in (.) do rem"') do (
  pushd "%~dp0"& <nul>"%~1_" set/p="%%i%%i  "& findstr/a:%c% . "%~1_*"
  (if "%~2" neq "/" echo.)& del "%~1_"& popd& set c=& exit/b
)
echo -----------------------------------------------------------------------

REM 2.5 БЛОК ПОДПРОГРАММЫ ДЛЯ ВЫВОДА ИНФОРМАЦИИ (начало)
:writer
set massage=%2
call :color %1
call :echo %2
@echo %DATE% %time:~0,8% : %massage:~1,-1% >> %logfile%& exit/b

REM 2.6 БЛОК ПОДПРОГРАММЫ ДЛЯ ПРОВЕРКИ, ПОСЛЕ ВЫПОЛНЕНИЯ ОПЕРАЦИЙ (начало)
:check_error
If %errorlevel%==0 (call :writer 2 "%textblock% %fileName%... Операция успешно завершена"
					goto :%nextPoint%) else (call :writer 4 "%textblock% %fileName%... Возникли проблемы при выполнении"
											 set answer=%nextPoint%
											 goto :askexit)
echo -----------------------------------------------------------------------
REM ========== 2.ОБЩИЕ КОМАНДЫ (конец) ==========

REM ========== 3.СБОРКА СТЕНДА (начало) =========
:extract
REM 3.1 РАСПАКОВЫВАЕМ АРХИВ SERVER.ZIP
If not exist "%winRar%" (
    echo ERROR: Архиватор WinRAR по пути "%winRar%" не найден. Проверьте путь, указанный в файле "%ini%"
	set success=1
	goto :exit_bat
)
	set "textBlock=Распаковываем архив server.zip"
	set "zipFile=%in%\server.zip"
	set "outputFile=%out%\"
	set "nextPoint=extract_client"
goto :unzip_data

:extract_client
REM 3.2 РАСПАКОВЫВАЕМ АРХИВ CLIENT.ZIP
	set "textBlock=Распаковываем архив client.zip"
	set "zipFile=%in%\client.zip"
	set "outputFile=%out%\client"
	set "nextPoint=extract_delphi"
echo Проверяем наличие директории client...
If not exist "%out%"\client (
		echo Директория client не обнаружена. Создаем директорию client...
		MD "%out%"\client
		goto :unzip_data) else (goto :unzip_data)

:extract_delphi
REM 3.3 ДОБАВЛЯЕМ DELPHI-КОМПОНЕНТЫ В директорию CLIENT
	set "textBlock=Добавляем Delphi-компоненты в директорию client"
	set "inputFolder=%DelphiXE%"
	set "outputFolder=%out%\client"
	set "nextPoint=extract_eis"
goto :copy_data

:extract_eis
REM 3.4 КОПИРУЕМ EIS.JAR
	set "textBlock=Копируем EIS.jar"
	set "inputFolder=%EIS%"
	set "outputFolder=%out%\server"
	set "nextPoint=extract_report"
goto :copy_data

:extract_report
REM 3.5 РАСПАКОВЫВАЕМ АРХИВ ОТЧЕТЫ
	set "textBlock=Распаковываем архив ОТЧЕТЫ"
	set "inputFolder=%REPORT%"
	REM Делаем проверку директории/архива
	if exist "%REPORT%\*.*" (
		set "zipFile=%REPORT%\*.zip") else (set "zipFile=%REPORT%")
	set "outputFile=%out%\"
	set "nextPoint=extract_sofit"
goto :unzip_data

:extract_sofit
REM 3.6 РАСПАКОВЫВАЕМ АРХИВ СОФИТ
	set "textBlock=Распаковываем архив СОФИТ"
	set "inputFolder=%SOFIT%"
	REM Делаем проверку директории/архива
	if exist "%SOFIT%\*.*" (
		set "zipFile=%SOFIT%\*.zip") else (set "zipFile=%SOFIT%")
	set "outputFile=%out%\"
	set "nextPoint=extract_shplan"
goto :unzip_data

:extract_shplan
REM 3.7 ДОБАВЛЯЕМ ФАЙЛЫ SCHPLANSTOP
	set "textBlock=Добавляем файлы schplanstop"
	set "inputFolder=%schplanstop%"
	set "outputFolder=%out%\SQL"
	set "nextPoint=extract_azkabat"
goto :copy_data

:extract_azkabat
REM 3.8 ДОБАВЛЯЕМ ФАЙЛЫ AZKA.BAT
	set "textBlock=Добавляем файлы azka.bat"
	set "inputFolder=%azka%"
	set "outputFolder=%out%"
	set "nextPoint=extract_port"
goto :copy_data

:extract_port
REM 3.9 ПЕРЕИМЕНОВЫВАЕМ ПОРТЫ
	set "textBlock=Переименовываем порты в"
	set "fileName=%infile_1%"
	set "fileDirectory=%out%"
	set "oldText=%SP_PORT%"
	set "newText=%renSP%"
	set "nextPoint=extract_port2"
goto :rename_data

:extract_port2
	set "fileName=%infile_1%"
	set "fileDirectory=%out%"
	set "oldText=%RMI_PORT%"
	set "newText=%renRMI%"
set "nextPoint=extract_port3"
goto :rename_data

:extract_port3
	set "fileName=%infile_2%"
	set "fileDirectory=%out%\XML"
	set "oldText=%SP_PORT%"
	set "newText=%renSP%"
	set "nextPoint=extract_port4"
goto :rename_data

:extract_port4
	set "fileName=%infile_3%"
	set "fileDirectory=%out%\client"
	set "oldText=%SP_PORT%"
	set "newText=%renSP%"
	set "nextPoint=extract_db"
goto :rename_data

:extract_db
REM 3.10 МЕНЯЕМ ПУТЬ ДО БД FIREBIRD В AZK2SERVER.PROPERTIES
	set "textBlock=Меняем путь до БД Firebird в"
	set "fileName=%infile_4%"
	set "fileDirectory=%out%"
	set "oldText=%fb_old%"
	set "newText=%fbNew%"
	set "nextPoint=extract_db2"
goto :rename_data

:extract_db2
REM 3.11.1 МЕНЯЕМ ПУТЬ ДО БД ORACLE В AZK2SERVER.PROPERTIES
	set "textBlock=Меняем путь до БД Oracle в"
	set "fileName=%infile_4%"
	set "fileDirectory=%out%"
	set "oldText=%ora_old%"
	set "newText=%oraNew%"
	set "nextPoint=extract_db3"
goto :rename_data

:extract_db3
REM 3.11.2 МЕНЯЕМ ПУТЬ ДО БД ORACLE В AZK2SERVER.PROPERTIES
	set "textBlock=Меняем путь до БД Oracle в"
	set "fileName=%infile_4%"
	set "fileDirectory=%out%"
	set "oldText=%ora_url_old%"
	set "newText=%oraUrlNew%"
	set "nextPoint=extract_xml"
goto :rename_data

:extract_xml
setlocal EnableDelayedExpansion
REM 3.12 МЕНЯЕМ ACTION="PERFORM" НА ACTION="SYNCHRONIZE"
call :writer E "Выполняем изменение action в XML. Определено %n% xml-файла..."
for /L %%s in (1,1,%n%) do (
	set param=var%%s
	set v1=%%s
	call :extract_listXML !param!
)
goto :extract_Xmx

:extract_listXML
	set "textBlock=Меняем action в"
	set "fileName=!%1!"
	set "fileDirectory=%out%\XML"
	set "oldText=perform"
	set "newText=synchronize"
	set "nextPoint=stopExit"
goto :rename_data

:stopExit
exit /b 0

:extract_Xmx
setlocal DisableDelayedExpansion
REM 3.13 МЕНЯЕМ РАЗМЕР ВЫДЕЛЕНОЙ ПАМЯТИ ДЛЯ JAVA
	set "textBlock=Меняем размер выделеной памяти для JAVA в файле"
	set "fileName=%infile_6%"
	set "fileDirectory=%out%\server\bin"
	set "oldText=%Jram_old%"
	set "newText=%javaNew%"
	set "nextPoint=extract_tomcat"
goto :rename_data

:extract_tomcat
REM 3.14 ДОБАВЛЯЕМ СБОРКУ ТОМКАТА, ПОДКЛАДЫВАЕМ AZK.WAR
	set "textBlock=Копируем сборку TOMCAT"
	set "fileName="
	set "inputFolder=%tomcat%"
	set "outputFolder=%out%\apache_tomcat"
	set "nextPoint=extract_tomcat2"
goto :copy_data

:extract_tomcat2
REM 3.14 ДОБАВЛЯЕМ СБОРКУ ТОМКАТА, ПОДКЛАДЫВАЕМ AZK.WAR
	set "textBlock=Копируем файл azk.war"
	set "inputFolder=%in%\azk.war"
	set "outputFolder=%out%\apache_tomcat\webapps"
	set "nextPoint=extract_tomcat_port"
goto :copy_data
									 
:extract_tomcat_port
REM 3.14.1 МЕНЯЕМ ПОРТ ВЕБ-СЕРВЕРА
	set "textBlock=Переименовываем порты в"
	set "fileName=%infile_7%"
	set "fileDirectory=%out%\apache_tomcat\conf"
	set "oldText=%Server_old%"
	set "newText=%ServerNew%"
	set "nextPoint=extract_tomcat_port2"
goto :rename_data

:extract_tomcat_port2
REM 3.14.2 МЕНЯЕМ ПОРТ ДЛЯ ДОСТУПА ВЕБ-ПРИЛОЖЕНИЯ
	set "fileName=%infile_7%"
	set "fileDirectory=%out%\apache_tomcat\conf"
	set "oldText=%Connector_old%"
	set "newText=%ConnectorNew%"
	set "nextPoint=startweb"
goto :rename_data

:startweb
REM 3.15 ЗАПУСКАЕМ ТОМКАТ ДЛЯ РАСПАКОВКИ azk.war
echo -----------------------------------------------------------------------
echo.
echo Запускаем томкат для распаковки azk.war
@echo.>>%logfile%
@echo %DATE% %time:~0,8% : Запускаем томкат для распаковки azk.war>>%logfile%
cd /d "%out%"\apache_tomcat\bin\
REM Использую call, из-за того, что после выполнения с командой start, текущая работа bat-ника прекращается.
call startup.bat

:ask_web_rmi
REM 3.16 МЕНЯЕМ RMI-ПОРТ В НАСТРОКАХ ТОМКАТА
echo -----------------------------------------------------------------------
echo.
echo МЕНЯЕМ RMI-ПОРТ В НАСТРОКАХ ТОМКАТА
@echo %DATE% %time:~0,8% : Меняем rmi-порт в настроках томката>>%logfile%
	set question="Изменить порт в файле "%infile_5%?" [y/n]: "
	set answer=web_rmi
goto :askexit

:web_rmi
echo -----------------------------------------------------------------------
echo.
cd /d "%out%\apache_tomcat\webapps\azk\WEB-INF\"
If exist %infile_5% (
		set "textBlock=Файл настроек найден. Переименовываем порты в"
		set "fileName=%infile_5%"
		set "fileDirectory=%out%\apache_tomcat\webapps\azk\WEB-INF\"
		set "oldText=%RMI_PORT%"
		set "newText=%renRMI%"
		set "nextPoint=ask_drop_war"
	goto :rename_data
) else (
	echo Файл настроек не найден.
		set question="Повторить поиск файла %infile_5%? [y/n]: "
		set answer=web_rmi
	goto :askexit)

:ask_drop_war
REM 3.17 УДАЛИТЬ ФАЙЛ AZK.WAR ИЗ СБОРКИ ТОМКАТА
echo -----------------------------------------------------------------------
echo.
set question="Удалить файл azk.war из сборки томката? [y/n]: "
set answer=drop_war
set answer2=ask_start
goto :askexit

:drop_war
cd /d "%out%"\apache_tomcat\bin\
call shutdown.bat
TIMEOUT /T 5 /NOBREAK
cd /d "%out%"\apache_tomcat\webapps
DEL /Q azk.war
If %errorlevel%==0 (call :writer 2 "Удаление файла azk.war... Успешно"
					) else (call :writer 4 "Удаление файла azk.war... Ошибка")
goto :ask_start

REM 3.18 ПРОВЕРКА НА ЗАПУСК СП АЦК-ФИНАНСЫ
:ask_start
echo -----------------------------------------------------------------------
echo.
set question="Запустить СП %SYS%? [y/n]: "
set answer=startbat
set answer2=1
goto :askexit

REM Запуск СП АЦК
:startbat
cd /d "%out%"
echo ----------------------------------------------------------------------->>%logfile%
@echo %DATE% %time:~0,8% : Сборка выполнена УСПЕШНО. Выполнен запуск СП %SYS%>>%logfile%
call :copyright
call StartServer.bat

REM ДЕЛАЕМ ОСТАНОВКУ ПЕРЕД ВЫХОДОМ. ПОСЛЕ ЧЕГО ВЫХОДИМ ИЗ ПРОГРАММЫ
:exit_bat
pause
echo ----------------------------------------------------------------------->>%logfile%
if %success%==0 (
	@echo %DATE% %time:~0,8% : Сборка выполнена УСПЕШНО. СП %SYS% не запускался >>%logfile%) else (
		@echo %DATE% %time:~0,8% : Сборка выполнена с ОШИБКАМИ >>%logfile%)

:copyright
@echo.>>%logfile%
@echo ===========================================================================>>%logfile%
@echo %titleName%>>%logfile%
@echo В случае проблем и вопросов обращаться к albafoxx (abzaev.albert@gmail.com)>>%logfile%
@echo ===========================================================================>>%logfile%

exit
END